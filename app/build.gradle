apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("private/keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    defaultConfig {
        applicationId "io.sodalic.blob"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 37       //TODO: update this version code for new releases
        versionName "0.0.1"  //TODO: update this version number for new releases
        setProperty("archivesBaseName", "SpaceBlob-$versionName")
        buildConfigField("String", "DEFAULT_STUDY_ID", "\"" + keystoreProperties['defaultStudyId'] + "\"")
        buildConfigField("String", "DEFAULT_SERVER_URL", "\"" + keystoreProperties['defaultServerUrl'] + "\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: ""]
            buildConfigField("boolean", "APP_IS_BETA", "false")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['releaseDSN'] + "\"")
            buildConfigField("boolean", "ALLOW_INSECURE_CONNECTION", "false")
        }
        beta {
            versionNameSuffix("-beta")
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: "-beta"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['betaDSN'] + "\"")
            buildConfigField("boolean", "ALLOW_INSECURE_CONNECTION", "true")
        }
        development {
            versionNameSuffix("-dev")
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: "-dev"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "true")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['developmentDSN'] + "\"")
            buildConfigField("boolean", "ALLOW_INSECURE_CONNECTION", "true")
            debuggable true
        }
    }
    flavorDimensions "customizableServerUrl"
    productFlavors {
        googlePlayStore {
            dimension "customizableServerUrl"
            manifestPlaceholders = [appName: "SpaceBlob"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", 'true')
            buildConfigField("boolean", "READ_TEXT_AND_CALL_LOGS", 'false')
        }
// TODO SG: remove altogether        
//        onnelaLabServer {
//            dimension "customizableServerUrl"
//            manifestPlaceholders = [appName: "Beiwe"]
//            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", "false")
//            buildConfigField("boolean", "READ_TEXT_AND_CALL_LOGS", 'true')
//        }
    }
    useLibrary "org.apache.http.legacy"
// TODO SG: remove altogether        
//    sourceSets {
//        onnelaLabServer {
//            manifest.srcFile 'src/onnelaLabServer/AndroidManifest.xml'
//        }
//    }
}
// Don't include the default "debug" build variant as an option
android.variantFilter { variant ->
    if (variant.buildType.name.equals('debug')) {
        variant.setIgnore(true);
    }
}

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.commonsware.cwac:anddown:0.3.0'
    implementation 'io.sentry:sentry-android:1.7.3'
    implementation 'com.madgag.spongycastle:core:1.54.0.0'
}
